<meta http-equiv="content-type" content="text/html; charset=UTF8">
<html>
  <head>
    <title>AgentScript Model</title>
    <script src="../agentscript.js"></script>
    <script src="../coffee-script.js"></script>
    <link rel="stylesheet" href="http://ajax.googleapis.com/ajax/libs/jqueryui/1.10.0/themes/base/jquery-ui.css" type="text/css" media="all" />
    <link href="//netdna.bootstrapcdn.com/twitter-bootstrap/2.3.1/css/bootstrap-combined.no-icons.min.css" rel="stylesheet">
    <link href="//netdna.bootstrapcdn.com/font-awesome/3.0.2/css/font-awesome.css" rel="stylesheet">
    <script src="http://ajax.googleapis.com/ajax/libs/jquery/1.9.0/jquery.min.js"></script>
    <script src="http://ajax.googleapis.com/ajax/libs/jqueryui/1.10.0/jquery-ui.min.js"></script>
    <script src="http://lab.concord.org/vendor/d3/d3.js"></script>
    <script src="../lab.grapher.js"></script>
    <script type="text/coffeescript">
    class ClimateModel extends ABM.Model
      u = ABM.util # static variable
      setup: -> # called by Model ctor
        @ticks = 1
        @refreshPatches = true
        @agents.setDefaultShape "arrow"
        @agentBreeds "sunrays heat IR CO2 clouds"

        # remove all existing agents
        while @agents.length
          @agents[@agents.length - 1].die()

        # globals
        @sunBrightness = 100
        @albedo = 0.3
        @temperature = 5
        @agentSize = 0.75
        @skyTop = (@patches.maxY) - 5
        @earthTop = 8 + @patches.minY
        @sunlightHeading = -1.1
        @numClouds = 0
        @hiding90 = false
        @showFPS = false

        # allow access to this model from window.ClimateModel
        window.ClimateModel = this

        @spacePatches =        (p for p in @patches when p.y == @patches.maxY)
        @skyTopPatches =       (p for p in @patches when p.y <  @patches.maxY && p.y > @skyTop)
        @skyPatches =          (p for p in @patches when p.y <= @skyTop && p.y > @earthTop)
        @earthSurfacePatches = (p for p in @patches when p.y == @earthTop)
        @earthPatches =        (p for p in @patches when p.y <  @earthTop)

        p.color = [0, 0, 0] for p in @spacePatches

        for p in @skyTopPatches
          p.color = [196, 196, 196] if p.y == @skyTop + 1
          p.color = [128, 128, 128] if p.y == @skyTop + 2
          p.color = [64, 64, 64]    if p.y == @skyTop + 3
          p.color = [32, 32, 32]    if p.y == @skyTop + 4

        p.color = [100, 150, 255] for p in @skyPatches
        p.color = [255, 200, 200] for p in @earthPatches
        @updateAlbedoOfSurface()
        @createVolcano()
        @createCO2(30)
        @createHeat(20)

      setAlbedo: (percent) ->
        @albedo = percent
        @updateAlbedoOfSurface()

      getAlbedo : ->
        @albedo

      setSunBrightness: (val) ->
        @sunBrightness = val

      getSunBrightness : ->
        @sunBrightness

      getTemperature : ->
        @temperature

      getCO2Count : ->
        @CO2().length

      addCO2: ->
        @createCO2( Math.max 1, Math.round @CO2().length*0.1 )

      subtractCO2: ->
        quant = Math.ceil @CO2().length*0.1
        while quant--
          @CO2().oneOf().die()

      updateAlbedoOfSurface: ->
        p.color = [Math.floor(196 * @albedo), Math.floor(255 * @albedo), Math.floor(196 * @albedo)]   for p in @earthSurfacePatches

      reflectOffHorizontalPlane: (a) ->
        heading = a.heading
        newheading = heading
        if heading > Math.PI
          newheading = Math.PI - (heading - Math.PI)
        if heading < Math.PI
          newheading = Math.PI + (Math.PI - heading)
        else
          newheading = 0
        a.heading = newheading

      headingUp: (a) ->
        heading = a.heading % Math.PI*2
        heading > 0 && heading < Math.PI

      transformToIR: (a) ->
        a.breed = "IR"
        a.heading = -@sunlightHeading
        # a.heading = u.randomFloat2(2.6, 0.5)
        # a.heading = u.randomCentered(Math.PI/4) + Math.PI/2
        a.color = [200, 32, 200]
        a.shape = "arrow"

      transformToHeat: (a) ->
        a.breed = "heat"
        a.y = @earthTop-1
        a.heading = u.randomFloat2(-0.5, -Math.PI+0.5)
        a.shape = "circle"
        randomLightness = u.randomInt2(32, 128)
        a.color = [255, randomLightness, randomLightness]

      #
      # CO2
      #
      createCO2: (num, location, heading) ->
        while num--
          @agents.create 1, (a) =>
            a.breed = "CO2"
            a.size = @agentSize
            a.color = [0, 255, 0]
            a.shape ="pentagon"
            a.heading = if heading? then heading else u.randomCentered(Math.PI)
            [x,y] = if location? then location else @getRandomLocation(@earthTop+1, @skyTop)
            a.setXY x, y

      runCO2: ->
        for a in @CO2()
          if a
            a.heading = a.heading + u.randomCentered(Math.PI/9)
            a.forward 0.1
            if a.y <= (-14)
              a.heading = u.randomFloat2(0.1, Math.PI-0.1)
            if a.y <= @earthTop + 1
              a.heading = u.randomFloat2(Math.PI/4, Math.PI*3/4)
            if a.y >= @skyTop + 1
              a.heading = u.randomFloat2(-Math.PI/4, -Math.PI*3/4)

      #
      # IR
      #
      runIR: ->
        for a in @IR()
          if a
            a.forward 0.5
            if @CO2().inRadius(a, 1).any()
              a.heading = u.randomFloat2(-Math.PI/4, -Math.PI*3/4)
            a.die() if a.heading == -@sunlightHeading && a.y > (14)
            if a.y <= @earthTop
              @transformToHeat(a)

      #
      # Heat
      #
      runHeat: ->
        @updateTemperature()
        for a in @heat()
          if a
            a.heading = a.rotate(u.randomCentered(0.3))
            a.forward u.randomFloat2(0.05, 0.2)
            if a.y <= @patches.minY
              a.heading = u.randomFloat2(0.1, Math.PI-0.1)
            if a.y >= @earthTop
              if @returnToSky
                @transformToIR(a)
              else
                a.heading = u.randomCentered(2)

      returnToSky: ->
        u.randomInt(100) < (temperature * 20) && u.randomInt(20) < 2

      updateTemperature: ->
        @temperature = 0.99 * @temperature + 0.01 * (-7 + 0.5 * @heat().length)

      leaveToSpace: (a) ->
        heading = a.heading % Math.PI
        ypos = a.y
        if heading < Math.PI && heading > 0
          if ypos < @patches.minY || ypos >= @patches.maxY
            a.die()

      createHeat: (num) ->
        while num--
          [x,y] = @getRandomLocation(@patches.minY, @earthTop)
          @agents.create 1, (a) =>
            a.breed = "heat"
            a.size = @agentSize
            a.heading = u.randomFloat2(-0.5, -Math.PI+0.5)
            a.shape = "circle"
            randomLightness = u.randomInt2(32, 128)
            a.color = [255, randomLightness, randomLightness]
            a.setXY x, y


      #
      # Clouds
      #
      addCloud: ->
        @numClouds++
        @setupClouds(@numClouds)

      subtractCloud: ->
        @numClouds = Math.max @numClouds-1, 0
        @setupClouds(@numClouds)

      setupClouds: (num) ->
        hiddenClouds = {}
        for a in @clouds()
          if a.hidden then hiddenClouds[a.cloudNum] = true
          a.die()
        numHiddenClouds = Object.keys(hiddenClouds).length
        i = 0
        while i < num
          @makeCloud(i, num, i < numHiddenClouds)
          i++

      hide90Clouds: ->
        cloudsToHide = Math.floor @numClouds * 0.9
        hiddenClouds = {}
        for a in @clouds()
          cloudNum = a.cloudNum
          if hiddenClouds[cloudNum] or Object.keys(hiddenClouds).length < cloudsToHide
            a.hidden = true
            hiddenClouds[cloudNum] = true

      makeCloud: (cloudNum, total, hidden) ->
        width = @skyTop - @earthTop
        mid = (@skyTop + @earthTop)/2
        y = mid + width * ((cloudNum/total) - 0.3) - 2
        y = 6 if cloudNum == 0
        x = 2 * u.randomFloat(@patches.maxX) + @patches.minX
        cloudParts = 3 + u.randomInt(16)
        while cloudParts--
          @agents.create 1, (a) =>
            a.breed = "clouds"
            a.cloudNum = cloudNum
            a.color = [255,255,255]
            a.size = @agentSize + 0.5 + u.randomFloat(1)
            a.shape = "circle"
            a.heading = 0
            a.hidden = hidden
            a.setXY x + u.randomFloat(5) - 4,  y + (u.randomFloat(u.randomFloat(3)))

      runClouds: ->
        for a in @clouds()
          if a
            a.forward 0.3 * (0.1 + (3 + a.cloudNum) / 10)

      #
      # Sunshine
      #
      runSunshine: ->
        for a in @sunrays()
          if a
            a.forward 0.5
            @leaveToSpace(a)
        @createSunshine()
        @reflectSunshineFromClouds()
        @encounterEarth()

      reflectSunshineFromClouds: ->
        for a in @sunrays()
          if a
            if @clouds().inRadius(a, 1).any()
              heading = u.randomFloat2(Math.PI/4, Math.PI*3/4)
              if @headingUp a
                heading = -heading
              a.heading = heading

      encounterEarth: ->
        for a in @sunrays()
          if a.y <= @earthTop
            if @albedo * 100 > u.randomInt(100)
              @reflectOffHorizontalPlane(a)
            else
              @transformToHeat(a)

      createSunshine: ->
        modelWidth = @patches.maxX - @patches.minX
        if 0.1 * @sunBrightness > u.randomInt(50)
          @agents.create 1, (a) =>
            a.breed = "sunrays"
            a.size = @agentSize
            a.color = [255,255,0]
            a.heading = @sunlightHeading
            a.setXY @patches.minX + u.randomFloat(modelWidth), @patches.maxY
            a.hidden = unless @hiding90 and Math.random() > 0.1 then false else true

      addSunraySpotlight: ->
        # try to add spotlight to a sunray at very top heading downwards
        foundOne = false
        for a in @sunrays().shuffle()
          if not @headingUp(a) and a.y > @patches.maxY-5
            foundOne = true
            @setSpotlight a
            break
        if not foundOne
          # if we did not find one, add spotlight to random sunray
          @setSpotlight "sunrays"

      #
      # Volcano
      #
      createVolcano: ->
        @agents.create 1, (a) =>
          a.size = 7
          a.color = [188, 140, 56]
          a.shape ="triangle"
          a.heading = Math.PI / 2
          a.setXY -17, -3
        @agents.create 1, (a) =>
          a.size = 3
          a.color = [255, 255, 255]
          a.shape ="triangle"
          a.heading = Math.PI / 2
          a.setXY -17, -1
        @agents.create 1, (a) =>
          a.size = 6
          a.color = [100, 150, 255]
          a.shape ="circle"
          a.setXY -17, 1

      erupt: ->
        for i in [0...15]
          @createCO2 1, [-17, -1.5], Math.PI/2

      #
      # Global Functions
      #
      hide90: ->
        @hiding90 = true
        for agentSet in [@sunrays(), @CO2(), @IR(), @heat()]
          for a in agentSet[Math.ceil(agentSet.length/10)..]
            a.hidden = true

        # have to do clouds seperately
        @hide90Clouds()

      showAll: ->
        @hiding90 = false
        for a in @agents
          a.hidden = false

      getRandomLocation: (minY, maxY) ->
        [
          u.randomFloat2 @patches.minX, @patches.maxX
          u.randomFloat2 minY, maxY
        ]

      #
      # Main Model Loop
      #
      step: ->
        @runSunshine()
        @runClouds()
        @runHeat()
        @runIR()
        @runCO2()

    # divName, patchSize, minX, maxX, minY, maxY, isTorus = false, topLeft=[10,10]
    #   NL Defaults: 13, -16, 16, -16, 16
    APP=new ClimateModel "layers", 12, -24, 24, -15, 15, true #use torus
    </script>

    <style type="text/css">
      body { font: 13px sans-serif; }
      #content {
        margin: 0em;
        padding: 0em; }
      p { font-size: 1.5em;
        margin-left: 1.0em }
      ul {
        list-style-type: none;
        margin: 0.3em 0em;
        padding-left: 0em;
        width: 100%; }
        ul li {
          display: table-cell;
          vertical-align: middle;
          margin: 0em;
          padding: 0em 0.3em 0em 0.3em; }
      #model {
        display: table-cell;
        margin: 0em 0.5em 0em 0.5em;
        width: 600px; }
      #layers {
        position: absolute;
        top: 10px;
        left: 10px;
        margin: 0em;
        padding: 0em; }
      #playback-controls {
        position: absolute;
        top: 385px;
        left: 10px;
        width: 588px;
        font-size: 1.1em;
        display: inline-block;
        background-color: #f8f8f8; }
      #playback-controls li, button, span {
        font-size: 1.1em; }
      #controls {
        display: table-cell;
        vertical-align: top;
        margin: 0em 0.5em 0em 0.5em;
        padding: 0em;
        font-size: 1.0em; }
      #controls label, span {
        font-size: 1.0em;
        vertical-align: top; }
      #controls button {
        font-size: 1.0em;
        vertical-align: middle; }
      #controls .output {
        font-size: 1.0em;
        vertical-align: middle; }
      #controls span.digits3 {
        font-size: 1.0em;
        margin: 0em;
        padding: 0em;
        float: right;
        text-align: left;
        width: 2em; }
      #controls span.slider-units {
        font-size: 90%;
        font-style: italic; }

      div.graph {
         background-color: #ddf2ff;
         border-radius: 0.25em;
         width: 360px;
         height: 140px;
      }

      .graph {
        position: relative;
        background: rgba(255, 255, 255, 0); }
        .graph:focus {
          outline: #76d2b8 solid 0.2em; }
        .graph div.background {
          position: absolute;
          background: #efefef; }
        .graph div.button-layer {
          position: absolute;
          text-align: right; }
          .graph div.button-layer a {
            opacity: 0.3;
            color: black;
            font-size: 120%; }
          .graph div.button-layer a:hover {
            opacity: 1; }
        .graph canvas.overlay {
          position: absolute;
          background: rgba(0, 255, 0, 0.05); }

      svg.graph:focus {
        outline: #76d2b8 solid 0.2em; }
      svg.graph text.title {
        font-size: 0.9em; }
      svg.graph text.ylabel {
        font-size: 0.8em; }
      svg.graph text.xlabel {
        font-size: 0.8em; }
      svg.graph text.axis {
        font-size: 0.8em; }
        svg.graph text.axis:focus {
          outline: #76d2b8 solid 0.2em; }
      svg.graph rect.draggable-axis {
        fill: rgba(255, 255, 255, 0); }
        svg.graph rect.draggable-axis:hover {
          fill: rgba(196, 196, 196, 0.2); }

      circle, .line {
        fill: none;
        stroke: steelblue;
        stroke-width: 2px; }
      circle {
        fill: white;
        fill-opacity: 0.2;
        cursor: move; }
        circle.selected {
          fill: #ff7f0e;
          stroke: #ff7f0e; }
        circle:hover {
          fill: #ff7f0e;
          stroke: #707f0e; }
        circle.selected:hover {
          fill: #ff7f0e;
          stroke: #ff7f0e; }
    </style>
  </head>
  <body onload="ABM.model.start(); setupControls();">
    <div id="content">
      <div id="model">
        <canvas id="canvas" >Your browser does not support HTML5 Canvas.</canvas>
        <div id="layers"></div>
      </div>
      <div id="controls">
        <div id='temperature-graph' class="graph"></div>
        <div id='co2-graph' class="graph"></div>
        <ul>
          <li>
            <button id="add-co2-button">Add CO2</button>
            <button id="subtract-co2-button">Subtract CO2</button>
          </li>
          <li>
            <span id="co2-output" class="output digits3"></span>
          </li>
          <li class="output">
            Temperature: <span id="temperature-output"></span>
          </li>
        </ul>
        <ul>
          <li>
            <label for="albedo-slider">Albedo:</label>
            <span class="slider-units">0 <input id="albedo-slider" type="range" min="0" max="1" step="0.01"/> 1</span>
          </li>
        </ul>
        <ul>
          <li>
            <label for="sun-brightness-slider">Sun Brightness:</label>
            <span class="slider-units">0 <input id="sun-brightness-slider" type="range" min="0" max="200" step="1"/> 200</span>
          </li>
        </ul>
        <ul>
          <li>
            <button id="add-clouds-button">Add Cloud</button>
            <button id="subtract-clouds-button">Subtract Cloud</button>
          </li>
          <li>
            <span id="co2-output" class="output digits3"></span>
          </li>
        </ul>
        <ul>
          <li>
            <button id="follow-sunray-button">Follow Energy Packet</button>
            <button id="follow-co2-button">Follow CO2</button>
          </li>
        </ul>
        <ul>
          <li>
            <button id="hide-button">Hide 90% of elements</button>
          </li>
          <li>
            <button id="erupt-button">Erupt!</button>
          </li>
        </ul>
      </div>
      <div id="playback-controls">
        <ul>
          <li>
            <button id="reset-button">Reset</button>
          </li>
          <li>
            <button id="play-button">Play</button>
          </li>
          <li>
            <button id="step-button">Step</button>
          </li>
          <li>
            <button id="stop-button">Stop</button>
          </li>
          <li>
            <span class="slider-units">Slow <input id="speed-slider" type="range" min="0" max="10" step="1"/> Fast</span>
          </li>
          Model Ticks: <span id="tick-counter"></span>
        </ul>
      </div>
    </div>
    <script>
      var addCO2Button = document.getElementById("add-co2-button"),
          subtractCO2Button = document.getElementById("subtract-co2-button"),
          albedoSlider = document.getElementById("albedo-slider"),
          sunBrightnessSlider = document.getElementById("sun-brightness-slider"),
          speedSlider = document.getElementById("speed-slider"),
          co2Output = document.getElementById("co2-output"),
          temperatureOutput = document.getElementById("temperature-output"),
          addCloudsButton = document.getElementById("add-clouds-button"),
          subtractCloudsButton = document.getElementById("subtract-clouds-button"),
          followSunrayButton = document.getElementById("follow-sunray-button"),
          followCO2Button = document.getElementById("follow-co2-button"),
          hideButton = document.getElementById("hide-button"),
          eruptButton = document.getElementById("erupt-button"),
          resetButton = document.getElementById("reset-button"),
          playButton = document.getElementById("play-button"),
          stopButton = document.getElementById("stop-button"),
          stepButton = document.getElementById("step-button"),
          tickCounter = document.getElementById("tick-counter"),
          temperatureFormatter = d3.format("3.1f"),
          countFormatter = d3.format("3f"),
          ticks = 0,
          lastTick = 0,
          temperatureGraph,
          co2Graph;

      function setupControls() {
        albedoSlider.value = ClimateModel.getAlbedo();
        sunBrightnessSlider.value = ClimateModel.getSunBrightness();
        speedSlider.value = 5;
      }

      addCO2Button.onclick = function() {
        ClimateModel.addCO2();
      }

      subtractCO2Button.onclick = function() {
        ClimateModel.subtractCO2();
      }

      addCloudsButton.onclick = function() {
        ClimateModel.addCloud();
      }

      subtractCloudsButton.onclick = function() {
        ClimateModel.subtractCloud();
      }

      followSunrayButton.onclick = function() {
        ClimateModel.addSunraySpotlight();
      }

      followCO2Button.onclick = function() {
        if (this.textContent === "Follow CO2") {
          ClimateModel.setSpotlight("CO2");
          this.textContent = "Stop following";
        } else {
          ClimateModel.removeSpotlight();
          this.textContent = "Follow CO2";
        }
      }

      hideButton.onclick = function() {
        if (this.textContent === "Hide 90% of elements") {
          ClimateModel.hide90();
          this.textContent = "Show all elements";
        } else {
          ClimateModel.showAll();
          this.textContent = "Hide 90% of elements";
        }
      }

      eruptButton.onclick = function() {
        ClimateModel.erupt();
      }

      albedoSlider.onchange = function() {
        ClimateModel.setAlbedo(+albedoSlider.value);
      }

      sunBrightnessSlider.onchange = function() {
        ClimateModel.setSunBrightness(+sunBrightnessSlider.value);
      }

      speedSlider.onchange = function() {
        // scale value, 0=10, 5=60, 10=500
        var val = speedSlider.value,
            frequency = val <= 5 ? (val * 10)+10 : (val-5)*88 + 60;
        ClimateModel.setStepFrequency(frequency);
      }

      resetButton.onclick = function() {
        lastTick = 0;
        temperatureGraph.reset();
        co2Graph.reset();
        ClimateModel.setup();
      }

      playButton.onclick = function() {
        ClimateModel.start();
      }

      stopButton.onclick = function() {
        ClimateModel.stop();
      }

      stepButton.onclick = function() {
        ClimateModel.stop();
        ClimateModel.animate();
        updateTickCounter();
      }

      function updateTickCounter() {
        ticks = ClimateModel.ticks;
        tickCounter.textContent = ticks;
      }

      temperatureGraph = Lab.grapher.Graph('#temperature-graph',
        {
          title:  "Temperature vs Time (model ticks)",
          xlabel: "Time (ticks)",
          ylabel: "Temperature",
          xmax:   2000,
          xmin:   0,
          ymax:   40,
          ymin:   -10,
          xTickCount: 4,
          yTickCount: 5,
          xFormatter: "3.3r",
          sample: 1,
          realTime: true,
          fontScaleRelativeToParent: true
        });

      co2Graph = Lab.grapher.Graph('#co2-graph',
        {
          title:  "CO2 vs Time (model ticks)",
          xlabel: "Time (ticks)",
          ylabel: "CO2",
          xmax:   2000,
          xmin:   0,
          ymax:   100,
          ymin:   0,
          xTickCount: 4,
          yTickCount: 5,
          xFormatter: "3.3r",
          sample: 1,
          realTime: true,
          fontScaleRelativeToParent: true
        });

      lastTick = 0;

      d3.timer(function(elapsed) {
        if (typeof ClimateModel !== "undefined") {
          var temperature = ClimateModel.getTemperature(),
              co2Count = ClimateModel.getCO2Count(),
              tick = ClimateModel.ticks,
              ticksElapsed = tick - lastTick;
          temperatureOutput.textContent = temperatureFormatter(temperature);
          co2Output.textContent = countFormatter(co2Count);
          if (!ClimateModel.animStop && ticksElapsed) {
            while (ticksElapsed--) {  // duplicate data if multiple model steps passed
              temperatureGraph.addPoints([temperature]);
              co2Graph.addPoints([co2Count]);
            }
            updateTickCounter();
            lastTick = tick;
          }
        }
      });

    </script>
  </body>
</html>
